buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        mirror {
        }
    }
    buildToolsVersion "23.0.1"
    compileSdkVersion 23

    defaultConfig {
        applicationId "com.morristaedt.mirror"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 2
        versionName "1.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            ext.enableCrashlytics = false
            applicationIdSuffix '.debug'
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.mirror
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.ahorn:android-rss:cf40b89'
    compile 'com.google.android.gms:play-services-vision:8.3.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }

    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
}

def signingFile = rootProject.file('signing.properties')
if (signingFile.exists()) {
    def props = new Properties()
    props.load(new FileInputStream(signingFile))
    android.signingConfigs.mirror.storeFile rootProject.file(props.keyStore)
    android.signingConfigs.mirror.storePassword props.keyStorePassword
    android.signingConfigs.mirror.keyAlias props.keyAlias
    android.signingConfigs.mirror.keyPassword props.keyAliasPassword
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.uphyca.gradle:gradle-android-aspectj-plugin:0.9.14'
    }
}
apply plugin: 'android-aspectj'

